# This recipe makes up for a deficiency in the main `prebuilt-guest-vm-package` recipe,
# which is only really geared for importing pre-built Linux distributions generated by Yocto.
#
# Namely, the prebuilt-guest-vm-package will always fail when there is not a separate kernel image
# and rootfs. However, in many "cloud bundle"-style images, such as those for Ubuntu by Canonical,
# the kernel is bundled directly under /boot in the rootfs.
#
# Somewhat confusingly, it's easiest to bypass EWAOL's VM management completely, and just install
# a Xen configuration to ${D}${sysconfdir}/xen/auto

SUMMARY = "This recipe provides an EWAOL Xen Guest VM configuration for Ubuntu 20.04.5 LTS"
LICENSE = "MIT"
LIC_FILES_CHKSUM = "\
    file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302 \
    "

SRC_URI:append = " \
    https://raw.githubusercontent.com/benmordaunt/sdv-hackathon-cfg3/main/xencfg/ubuntu-xenguest.conf;sha256sum=208fbefe3410b79296d51116a266db2bb718ae5db58ec2c4761000f910603530\
    https://github.com/benmordaunt/sdv-hackathon-cfg3/raw/main/prebuilt/images/focal-server-cloudimg-arm64.raw.img.bz2;sha256sum=c6c0bd96f291864d50681d9fc34a4766b48ac2902e3d383525e0e88eb25c8964\
"

inherit allarch
inherit features_check
REQUIRED_DISTRO_FEATURES += "ewaol-virtualization"

DEPENDS += "dosfstools-native mtools-native"

do_configure[noexec] = "1"
do_compile[noexec] = "1"

do_install() {
    CFG_NAME="ubuntu-xenguest.conf"
    DISK_NAME="focal-server-cloudimg-arm64.raw.img"
    DISK_DST="${datadir}/guest-vms/ubuntu-xenguest/focal-server-cloudimg-arm64.img"
    DISK_DIRNAME=$(dirname ${DISK_DST})
    
    install -d ${D}${sysconfdir}/xen/auto
    install -Dm 0640 ${WORKDIR}/${CFG_NAME} ${D}${sysconfdir}/xen/auto/${CFG_NAME}

    install -d ${D}${DISK_DIRNAME}
    install -Dm 0640 ${WORKDIR}/${DISK_NAME} ${D}${DISK_DST}
    
    if [[ -n "${HKT_UBUNTU_NOCLOUD}" ]]; then
        SEED_DST=${DISK_DIRNAME}/focal-server-cloudimg-arm64-seed.img

        echo -e "instance-id: bcx22-ubuntu\nlocal-hostname: bcx22-ubuntu" > ${WORKDIR}/meta-data
	echo -e "#cloud-config\npassword: bcx22\nchpasswd: { expire: False }\nssh_pwauth: True\n" > ${WORKDIR}/user-data

        truncate --size 2M ${WORKDIR}/seed.img
	mkfs.vfat -n cidata ${WORKDIR}/seed.img

	mcopy -oi ${WORKDIR}/seed.img ${WORKDIR}/user-data ${WORKDIR}/meta-data

	install -Dm 0640 ${WORKDIR}/seed.img ${D}${SEED_DST}

	sed -i 's/%%HKT_UBUNTU_NOCLOUD_SEED_IMAGE%%/, \'file:\/usr\/share\/guest-vms\/ubuntu-xenguest\/focal-server-cloudimg-arm64-seed.img,xvdb,r\'/' ${D}${sysconfdir}/xen/auto/${CFG_NAME}
    else
        sed -i 's/%%HKT_UBUNTU_NOCLOUD_SEED_IMAGE%%//' ${D}${sysconfdir}/xen/auto/${CFG_NAME}
    fi

    #  Ideally, 
}

FILES:${PN} = "${datadir} ${sysconfdir}"
